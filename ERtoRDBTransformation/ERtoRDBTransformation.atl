-- @path ERModel=/ERMetamodel/model/ERMetaModel.ecore
-- @path RDBModel=/RDBMetamodel/model/RDBMetaModel.ecore

module ERtoRDBTransformation;

create OUT : RDBModel from IN : ERModel;



-- Transform each Entity to a Table
lazy rule Entity2Table {
    from
        e : ERModel!Entity 
    to
        t : RDBModel!Table (
            name <- e.name,
            columns <- e.attributes->collect(a | thisModule.Attribute2Column(a)),
            primaryKey <- thisModule.Key2PrimaryKey(e.attributes->select(a | a.isPrimary))
        )
}

-- Helper function to map ERModel types to RDBModel types
helper def : mapType(type : ERModel!DataType) : RDBModel!DataType =
    if type= #ERNumber then
        #RDBInteger
    else 
    	if type = #ERDecimal then
        	#RDBReal
	    else 
	    	if type = #ERText then
		        #RDBString
		    else 
		    	if type = #ERBoolean then
			        #RDBBoolean
			    else
			    	if type = #ERDate then
				        #RDBDate
				    else
				    	if type = #ERTime then
					        #RDBTime
					    else
					        type
						endif
					endif
				endif
			endif
		endif
    endif;

-- Transform Attributes to Columns
unique lazy rule Attribute2Column {
    from
        a : ERModel!Attribute
    to
        c : RDBModel!Column (
            name <- a.name,
            type <- thisModule.mapType(a.type),
            isNullable <- a.isNullable,
			isUnique <- a.isUnique
        )
}

unique lazy rule Key2PrimaryKey {
    from
        pkAttributes : Sequence(ERModel!Attribute)  -- List of primary key attributes
    to
        pk : RDBModel!PrimaryKey (
            columns <- pkAttributes->collect(a | thisModule.Attribute2Column(a))
        )
}

-- Handle 1-to-many Relationships by Creating Foreign Keys (source: "one", target: "many")
rule OneToManyRelationship2ForeignKey {
    from
        r : ERModel!Relationship (r.isOneToMany())
    to
        fk : RDBModel!ForeignKey (
            sourceTable <- r.target,  -- "many" side (target)
            targetTable <- r.source,  -- "1" side (source)
            sourceColumn <- thisModule.ForeignKeyColumn(r.source),
			targetColumn <- thisModule.Attribute2Column(r.source.attributes->select(a | a.isPrimary).first())
        )
}

-- Handle many-to-1 Relationships by Creating Foreign Keys (source: "many", target: "one")
rule ManyToOneRelationship2ForeignKey {
    from
        r : ERModel!Relationship (r.isManyToOne())
    to
        fk : RDBModel!ForeignKey (
            sourceTable <- r.source,  -- "many" side (source)
            targetTable <- r.target,  -- "1" side (target)
            sourceColumn <- thisModule.ForeignKeyColumn(r.target),
			targetColumn <- thisModule.Attribute2Column(r.target.attributes->select(a | a.isPrimary).first())
        )
	do {
        -- Access the target table using resolveTemp and add the foreign key column
        let tt : RDBModel!Table = thisModule.resolveTemp(r.target, 't') in
            tt.columns <- tt.columns->union(Set{thisModule.ForeignKeyColumn(r.target)});
    }
}

-- , -- here
--        updatedTargetTable : RDBModel!Table (
--            name <- thisModule.resolveTemp(r.source, 't').name,  -- Use the name of the target table
--			primaryKey <- thisModule.resolveTemp(r.source, 't').primaryKey,
--			foreignKeys <- thisModule.resolveTemp(r.source, 't').foreignKeys->union(Set{fk}),
--            columns <- thisModule.resolveTemp(r.source, 't').columns->union(Set{thisModule.ForeignKeyColumn(r.target)})  -- Add fkColumn to columns
--        )

-- Handle Many-to-many Relationships by Creating a Join Table with Composite Primary Key and Foreign Keys
rule ManyToManyRelationship2JoinTable {
    from
        r : ERModel!Relationship (r.isManyToMany())
    to
        joinTable : RDBModel!Table (
            name <- r.source.name + '_' + r.target.name + '_Join',
            columns <- Sequence {
                thisModule.ForeignKeyColumn(r.source),
                thisModule.ForeignKeyColumn(r.target)
            },
            primaryKey <- thisModule.Key2PrimaryKey(
                Sequence {
                    thisModule.ForeignKeyColumn(r.source),
                    thisModule.ForeignKeyColumn(r.target)
                }
            ),
            foreignKeys <- Sequence {
                thisModule.CreateForeignKeyForJoinTable(
                    joinTable,
                    r.source,
                    thisModule.ForeignKeyColumn(r.source)
                ),
                thisModule.CreateForeignKeyForJoinTable(
                    joinTable,
                    r.target,
                    thisModule.ForeignKeyColumn(r.target)
                )
            }
        )
}

-- Helper rule to create ForeignKey object for each side of the Join Table
unique lazy rule CreateForeignKeyForJoinTable {
    from
        joinTable : RDBModel!Table,
        targetEntity : ERModel!Entity,
        sourceColumn : RDBModel!Column
    to
        fk : RDBModel!ForeignKey (
            sourceTable <- joinTable,                                  -- Set to the join table
            targetTable <- targetEntity,      -- Set to the original table of the target entity
            sourceColumn <- sourceColumn,                              -- Column in the join table
            targetColumn <- thisModule.Attribute2Column(targetEntity.attributes->select(a | a.isPrimary).first())
        )
}

-- Helper to create a ForeignKey column
unique lazy rule ForeignKeyColumn {
	from
		targetEntity: ERModel!Entity
	to
		t: RDBModel!Column (
        name <- targetEntity.name + '_id',
        type <- #RDBInteger, -- Assuming INTEGER type for foreign keys
        isNullable <- false
    )
}

-- Helper for many-to-many relationships (returns true if many-to-many)
helper context ERModel!Relationship def : isManyToMany() : Boolean = 
    ((self.sourceCardinality.maxValue = -1 or self.sourceCardinality.maxValue > 1) and (self.targetCardinality.maxValue = -1 or self.targetCardinality.maxValue > 1));

-- Helper for one-to-many relationships (returns true if one-to-many)
helper context ERModel!Relationship def : isOneToMany() : Boolean = 
	self.sourceCardinality.maxValue = 1 and (self.targetCardinality.maxValue = -1 or self.targetCardinality.maxValue > 1);

-- Helper for many-to-one relationships (returns true if many-to-one)
helper context ERModel!Relationship def : isManyToOne() : Boolean = 
	(self.sourceCardinality.maxValue = -1 or self.sourceCardinality.maxValue > 1)  and self.targetCardinality.maxValue = 1;

-- Helper to retrieve the corresponding Table for an Entity
helper context ERModel!Entity def: getTable() : RDBModel!Table =
    RDBModel!Table.allInstances()->select(t | t.name = self.name)->first();
	


	
	